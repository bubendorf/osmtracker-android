apply plugin: 'com.android.application'

android {
    compileSdkVersion 16
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId "me.guillaumin.android.osmtracker"
        minSdkVersion 8
        targetSdkVersion 16

        testApplicationId "me.guillaumin.android.osmtracker.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    signingConfigs {


        // Create a file $HOME/.gradle/gradle.properties
        // containing the values signing.storeFile=..., etc.
        if (project.hasProperty('signing.storeFile')) {
            release {
                storeFile file(project.property('signing.storeFile'))
                storePassword project.property('signing.storePassword')
                keyAlias project.property('signing.keyAlias')
                keyPassword project.property('signing.keyPassword')
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            if (signingConfigs.hasProperty('release')) {
                signingConfig signingConfigs.release
            }


        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE.txt'
    }

}

dependencies {
    compile 'org.apache.james:apache-mime4j-core:0.8.1'
    compile 'org.apache.httpcomponents:httpclient:4.5.3'
    compile 'org.apache.httpcomponents:httpcore:4.4.6'
    compile 'org.apache.httpcomponents:httpmime:4.5.3'
    compile 'org.osmdroid:osmdroid-android:5.6.5'
    compile 'oauth.signpost:signpost-commonshttp4:1.2.1.2'
    compile 'oauth.signpost:signpost-core:1.2.1.2'
    compile 'org.slf4j:slf4j-android:1.7.25'
    // Acralyzer crash reports
//    compile('ch.acra:acra:4.10.0') {
    compile('com.faendir:acra:4.10.0') {
        exclude group: 'org.json'
    }

    androidTestCompile 'junit:junit:4.12'
    testCompile 'junit:junit:4.12'
}

task copyNorwegianValues(type: Copy) {
    from 'src/main/res/values-nb/'
    into 'src/main/res/values-no/'
}

task copyNorwegianXml(type: Copy) {
    from 'src/main/res/xml-nb/'
    into 'src/main/res/xml-no/'
}

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        // Copying 'nb' resources to 'no' (See pull request #6)
        output.processResources.dependsOn([copyNorwegianValues, copyNorwegianXml])
    }
}
